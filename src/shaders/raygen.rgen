#version 460
#extension GL_EXT_ray_tracing : enable

struct RayPayload {
	vec3 color;
	float distance;
	vec3 normal;
	float reflector;
};

layout(binding = 0, set = 0) uniform CameraProperties 
{
	mat4 viewInverse;
	mat4 projInverse;
} cam;

layout(binding = 1, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 2, set = 0, rgba8) uniform image2D image;

layout (constant_id = 0) const int MAX_RECURSION = 2;

layout(location = 0) rayPayloadEXT RayPayload rayPayload;




void main() 
{
	const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);
	vec2 d = inUV * 2.0 - 1.0;

	vec4 origin = cam.viewInverse * vec4(0,0,0,1);
	vec4 target = cam.projInverse * vec4(d.x, d.y, 1, 1) ;
	vec4 direction = cam.viewInverse*vec4(normalize(target.xyz), 0) ;

	float tmin = 0.001;
	float tmax = 10000.0;


    vec3 color = vec3(0.0);
	float energie = 1.;
	for (int i = 0; i < MAX_RECURSION; i++) {
		traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xff, 0, 0, 0, origin.xyz, tmin, direction.xyz, tmax, 0);
		energie *= rayPayload.reflector;
		vec3 hitColor = rayPayload.color;

		if (rayPayload.distance < 0.0f) {
			color = hitColor * energie;
			break;
		} else if ( energie >= 0.7f) {
			const vec4 hitPos = origin + direction * rayPayload.distance;
			origin.xyz = hitPos.xyz + rayPayload.normal * 0.001f;
			direction.xyz = reflect(direction.xyz, rayPayload.normal);
			color += (1-energie) * hitColor;
		} else {
			color += hitColor * energie;
			break;
		}
	}
	imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(color, 1.0));
}